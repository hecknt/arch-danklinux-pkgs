# This is a modified version of the official Arch Linux package that pulls the git repo directly.
# https://gitlab.archlinux.org/archlinux/packaging/packages/quickshell
# Maintainer: Hec <hec@heccraft.com>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=quickshell-git
_pkgname=quickshell
pkgver=0.2.0.r48.g26531fc
pkgrel=1
pkgdesc='Flexible toolkit for making desktop shells with QtQuick'
arch=(x86_64 aarch64)
url="https://git.outfoxxed.me/$_pkgname/$_pkgname"
license=(LGPL-3.0-only)
depends=(gcc-libs #libgcc_s.so libstdc++.so
         glibc # libc.so libm.so
         hicolor-icon-theme
         jemalloc libjemalloc.so
         libdrm # libdrm.so
         libglvnd libEGL.so libOpenGL.so
         libpipewire libpipewire-0.3.so
         libxcb # libxcb.so
         mesa # libgbm.so
         pam libpam.so
         polkit
         qt6-base # libQt6Core.so libQt6DBus.so libQt6Gui.so libQt6Network.so libQt6Widgets.so
         qt6-declarative # libQt6Qml.so libQt6Quick.so
         qt6-svg
         qt6-wayland # libQt6WaylandClient.so
         wayland libwayland-client.so)
makedepends=(cli11
             cmake
             ninja
             qt6-shadertools
             spirv-tools
             wayland-protocols
             git)
provides=("$_pkgname")
conflicts=("$_pkgname")
source=("git+$url.git")
sha256sums=('SKIP')

pkgver() {
  cd "$_pkgname"
  git describe --long --tags --abbrev=7 --exclude='*[a-zA-Z][a-zA-Z]*' \
    | sed -E 's/^[^0-9]*//;s/([^-]*-g)/r\1/;s/-/./g'
}

build() {
	cd "$_pkgname"
	local cmake_options=(
		-D CMAKE_BUILD_TYPE=RelWithDebInfo
		-D DISTRIBUTOR='Arch Linux'
		-D DISTRIBUTOR_DEBUGINFO_AVAILABLE=Yes
		-D CRASH_REPORTER=Off
		-D CMAKE_INSTALL_PREFIX=/usr
		-D INSTALL_QML_PREFIX=lib/qt6/qml
	)
	cmake -G Ninja -B build -W no-dev "${cmake_options[@]}"
	cmake --build build
}

package() {
	cd "$_pkgname"
	DESTDIR="$pkgdir" cmake --install build
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
