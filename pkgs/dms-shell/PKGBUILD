# Maintainer: Hec <hec@heccraft.com>

_pkg1=DankMaterialShell
pkgname=(dms-shell greetd-dms-greeter)
pkgver=1.0.3
pkgrel=2
arch=(x86_64 aarch64)
url="https://github.com/AvengeMedia/$_pkg1"
license=(GPL-3.0-only)
depends=(dgop
         inter-font
         quickshell
         ttf-fira-code
         ttf-material-symbols-variable
         accountsservice)
optdepends=('brightnessctl: Laptop display brightness control'
            'cava: Audio visualizer'
            'cliphist: Clipboard history functionality'
            'matugen: Dynamic wallpaper-based theming'
            'networkmanager: Required for network management'
            'qt5ct: Qt5 application theming'
            'qt6ct: Qt6 application theming'
            'wl-clipboard: Copy functionality for PIDs and other elements')
makedepends=(go
  git)
source=("git+$url.git" "greetd-dms-greeter.install" "dms-greeter-path.patch")
sha256sums=('SKIP'
            'fd72627b9b242c6b70c56acb2a8a7881d2cb5c5fe7c7edeed307eb3b223c72f0'
            '87ca0f53f542fa5525e9775a4b2eca93f97f87dfdc8aff3b4b974022df568a37')

prepare() {
  cd "$_pkg1"
  git checkout v$pkgver
  patch -Np1 -i "$srcdir"/dms-greeter-path.patch
}

build() {
  cd "$_pkg1/core"
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export CGO_LDFLAGS="$LDFLAGS"
  make dist VERSION=${pkgver}
}

package_dms-shell() {
  pkgdesc='A Quickshell-based desktop shell with Material 3 design principles'

  case "$arch" in
    x86_64)
      DMS_BINARY="dms-linux-amd64"
      ;;
    aarch64)
      DMS_BINARY="dms-linux-arm64"
      ;;
  esac

  cd "$_pkg1"
  ./core/bin/$DMS_BINARY completion bash > dms.bash
  ./core/bin/$DMS_BINARY completion zsh > dms.zsh
  ./core/bin/$DMS_BINARY completion fish > dms.fish
  install -Dm0755 ./core/bin/$DMS_BINARY "$pkgdir/usr/bin/dms"
  install -dm0755 "$pkgdir/usr/share/quickshell/dms"
  cp -r "quickshell/"* "$pkgdir/usr/share/quickshell/dms/"
  install -Dm0644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  cp -r "docs/"* "$pkgdir/usr/share/doc/$pkgname/"
  install -Dm0644 -t "$pkgdir/usr/lib/systemd/user/" assets/systemd/dms.service
  install -Dm644 dms.bash $pkgdir/usr/share/bash-completion/completions/dms
  install -Dm644 dms.zsh $pkgdir/usr/share/zsh/site-functions/_dms
  install -Dm644 dms.fish $pkgdir/usr/share/fish/vendor_completions.d/dms.fish
}

package_greetd-dms-greeter() {
  pkgdesc='DankMaterialShell greeter for greetd'
  depends+=(greetd dms-shell)
  install=greetd-dms-greeter.install
  backup=('etc/greetd/config.toml')
  optdepends+=(
    'niri: Niri compositor support'
    'hyprland: Hyprland compositor support'
    'sway: Sway compositor support'
    'mangowc: MangoWC compositor support'
  )

  cd "$_pkg1"/quickshell
  install -Dm755 "Modules/Greetd/assets/dms-greeter" "$pkgdir/usr/bin/dms-greeter"

  install -Dm644 "Modules/Greetd/README.md" "$pkgdir/usr/share/doc/dms-greeter/README.md"

  install -dm750 "$pkgdir/var/cache/dms-greeter"
  install -Dm644 "systemd/tmpfiles-dms-greeter.conf" "$pkgdir/usr/lib/tmpfiles.d/dms-greeter.conf"
}
