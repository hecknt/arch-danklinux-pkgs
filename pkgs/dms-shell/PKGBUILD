# Maintainer: Hec <hec@heccraft.com>

_pkg1=DankMaterialShell
pkgname=dms-shell
pkgver=0.6.2
pkgrel=4
pkgdesc='A Quickshell-based desktop shell with Material 3 design principles'
arch=(x86_64 aarch64)
url="https://github.com/AvengeMedia/$_pkg1"
license=(GPL-3.0-only)
depends=(dgop
         inter-font
         quickshell
         ttf-fira-code
         ttf-material-symbols-variable)
optdepends=('brightnessctl: Laptop display brightness control'
            'cava: Audio visualizer'
            'cliphist: Clipboard history functionality'
            'matugen: Dynamic wallpaper-based theming'
            'networkmanager: Required for network management'
            'qt5ct: Qt5 application theming'
            'qt6ct: Qt6 application theming'
            'wl-clipboard: Copy functionality for PIDs and other elements')
makedepends=(go)
_archive="$_pkg1-$pkgver"
source=("$url/archive/v$pkgver/$_archive.tar.gz")
sha256sums=('a9f78299d560f3e361fd32757b2ccf869036a9f7e4ae33462368284e8f90fa78')

build() {
  cd "$_archive/core"
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export CGO_LDFLAGS="$LDFLAGS"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
  go build -ldflags="-X main.Version=v$pkgver" -o dms ./cmd/dms
}

package() {
  cd "$_archive"
  ./core/dms completion bash > dms.bash
  ./core/dms completion zsh > dms.zsh
  ./core/dms completion fish > dms.fish
  install -Dm0755 -t "$pkgdir/usr/bin/" core/dms
  install -dm0755 "$pkgdir/etc/xdg/quickshell/dms"
  cp -r "quickshell/"* "$pkgdir/etc/xdg/quickshell/dms/"
  install -Dm0644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  cp -r "docs/"* "$pkgdir/usr/share/doc/$pkgname/"
  install -Dm0644 -t "$pkgdir/usr/lib/systemd/user/" quickshell/assets/systemd/dms.service
  install -Dm644 dms.bash $pkgdir/usr/share/bash-completion/completions/dms
  install -Dm644 dms.zsh $pkgdir/usr/share/zsh/site-functions/_dms
  install -Dm644 dms.fish $pkgdir/usr/share/fish/vendor_completions.d/dms.fish
}
